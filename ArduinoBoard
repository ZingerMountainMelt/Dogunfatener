#include <SPI.h>
#include <MFRC522.h>
#include "DFRobotDFPlayerMini.h"
#include "SoftwareSerial.h"

#define SS_PIN 10
#define RST_PIN 9
#define LED_G 8 //define green LED
#define LED_R 5 //define red LED
#define LED_Y 4 //define yellow LED

MFRC522 mfrc522(SS_PIN, RST_PIN);   // Create MFRC522 instance.

SoftwareSerial DFplayer(3, 2);
DFRobotDFPlayerMini myDFPlayer;

int randNumberO = 0;         //Used to randomize Oakley's mp3 files
int randNumberC = 0;         //Used to randomize Chaplin's mp3 files
int unknownMP3 = 3;          //Used to play mp3 file associated w/ unidentified tag (became file 3 from 7?)
int chaplinVolume = 25;
int oakleyVolume = 15;
int unknownVolume = 20;
 
void setup() 
{
  Serial.begin(9600);   // Initiate a serial communication
  SPI.begin();      // Initiate  SPI bus
  mfrc522.PCD_Init();   // Initiate MFRC522
  pinMode(LED_G, OUTPUT);
  pinMode(LED_R, OUTPUT);
  pinMode(LED_Y, OUTPUT);
  
  Serial.println("Scan a dog");
  
  DFplayer.begin(9600);
  myDFPlayer.begin(DFplayer);

}

void loop() 
{
  digitalWrite(LED_Y, HIGH);
  digitalWrite(LED_G, LOW);
  digitalWrite(LED_R, LOW);

  randNumberO = 0;
  randNumberC = 0;
  
  // Look for new cards
  if ( ! mfrc522.PICC_IsNewCardPresent()) 
  {
    return;
  }
  // Select one of the cards
  if ( ! mfrc522.PICC_ReadCardSerial()) 
  {
    return;
  }
  //Show UID on serial monitor
  Serial.print("UID tag :");
  String content= "";
  byte letter;
  for (byte i = 0; i < mfrc522.uid.size; i++) 
  {
     Serial.print(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " ");
     Serial.print(mfrc522.uid.uidByte[i], HEX);
     content.concat(String(mfrc522.uid.uidByte[i] < 0x10 ? " 0" : " "));
     content.concat(String(mfrc522.uid.uidByte[i], HEX));
  }
  Serial.println();
  Serial.print("Message : ");
  content.toUpperCase();
  
  if (content.substring(1) == "1A 08 93 7F")
  {
    Serial.println("Oakley");
    myDFPlayer.volume(oakleyVolume);       //Set volume low for Oakley to 
    randNumberO = random(1,3);             //set to randomize MP3 files (maximum exclusive) Files 1-2 not corrupt for Oakley
    Serial.print("Random Number: ");       //Display randomized number
    Serial.println(randNumberO);
    myDFPlayer.play(randNumberO);          //play randomized mp3 file
    digitalWrite(LED_Y, LOW);              //disable yellow led
    digitalWrite(LED_G, HIGH);             //Green LED for Oakley
    delay(10000);
    digitalWrite(LED_G, LOW);              //disable green after pause
    randNumberO = 0;                       //reset rand
  }
 
  else if (content.substring(1) == "69 85 87 C2")   //Chaplin's designated RFID tag
  {
    Serial.println("Chaplin");
    myDFPlayer.volume(chaplinVolume);      //Set volume high for Chaplin to alert me and scare him slightly
    randNumberC = random(4,7);             //set to randomize MP3 files. Files 4-6 chaplin designated
    Serial.print("Random Number: ");       //Display randomized number
    Serial.println(randNumberC);
    myDFPlayer.play(randNumberC);          //play randomized mp3 file
    digitalWrite(LED_Y, LOW);              //disable yellow led
    digitalWrite(LED_R, HIGH);             //Red LED for Chaplin
    delay(10000);
    digitalWrite(LED_R, LOW);              //disable red after pause
    randNumberC = 0;                       //reset rand
  }
  
  else {
    Serial.println("Unknown");
    myDFPlayer.volume(unknownVolume);
    Serial.print("Number: ");
    Serial.println(unknownMP3);
    myDFPlayer.play(unknownMP3);
    digitalWrite(LED_R, LOW);
    digitalWrite(LED_G, LOW);
    digitalWrite(LED_Y, HIGH);
    delay(200);
    digitalWrite(LED_Y, LOW);
    delay(200);
  }
}
